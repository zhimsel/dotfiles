#!/usr/bin/env zsh
# Remove git worktrees
# Given a worktree path, delete the worktree and any empty parent directories

# Optional arguments are one or more worktree paths; default is current worktree
for wt_path in "${@:-$(git rev-parse --show-toplevel)}"; do
  local wt_parent="$(realpath ${wt_path}/..)"

  # Save our place if removing the current worktree
  [[ -z $1 ]] && { cd "$(git rev-parse --git-common-dir)/../$(git rev-parse --show-prefix)" \
    || cd "$(git rev-parse --git-common-dir)/.." \
    || return $? }

  # Remove it! (and prune)
  if ! git worktree remove "$wt_path"; then
    if confirm "Forcibly remove it?"; then
      git worktree remove --force "$wt_path" || return $?
    else
      cd - >/dev/null
      return 1
    fi
  fi
  git worktree prune

  # Remove empty parent dirs
  rmdir --ignore-fail-on-non-empty --parents "$wt_parent"
done
