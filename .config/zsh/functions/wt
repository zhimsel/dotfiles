#!/usr/bin/env zsh
# Create git worktrees
# Given a branch name create and/or cd into a worktree

# Do nothing if no branch was provided
[[ -z "$1" ]] && return 1

# First argument is the branch name
local branch="$1"

# Start with a clean understanding of the current worktrees
git worktree prune

# Save where we are in the work tree, so we can stay in that directory when we
# switch work trees
local prefix="$(git rev-parse --show-prefix)"

# Grab the path of the target branch's worktree
local wt_path=$(git worktree list | awk -v branch="[$branch]" '$3==branch {print $1}')

# Create the worktree if it doesn't already exist
if [[ -z "$wt_path" ]]; then

  # Move up to the repo root
  cd "$(git rev-parse --git-common-dir)/.." || return $?

  # Optional second argument is the path where the worktree should be created.
  # Defaults to 'reponame.branchname'
  local wt_path="${2:-$(git rev-parse --show-toplevel).$branch}"

  git worktree add "$wt_path" "$branch" || return $?
fi

# Try to cd into the same dir we started in; otherwise, cd into the repo root
cd "$wt_path/$prefix" || cd "$wt_path" || return $?
